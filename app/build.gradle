plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}

android {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    namespace 'cz.chrastecky.aiwallpaperchanger'
    compileSdk 34

    defaultConfig {
        applicationId "cz.chrastecky.aiwallpaperchanger"
        minSdk 24
        targetSdk 34
        versionCode 32
        versionName '1.13.0'
        versionNameSuffix '-'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        dataBinding {
            enabled = true
        }
        buildConfigField "String", "MAINTAINER", "\"Rikudou\""
        buildConfigField "String", "ANONYMOUS_API_KEY", "\"0000000000\""
        buildConfigField "String", "SUPPORT_EMAIL", "\"" + (properties.getProperty("support_email") ?: "") + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    flavorDimensions 'nsfw', 'key', 'doze', 'billing'
    productFlavors {
        nsfwEnabled {
            dimension 'nsfw'
            versionNameSuffix 'N'
            buildConfigField "boolean", "NSFW_ENABLED", "true"
        }
        nsfwDisabled {
            dimension 'nsfw'
            versionNameSuffix 'P'
            buildConfigField "boolean", "NSFW_ENABLED", "false"
        }
        prodKey {
            dimension 'key'
            versionNameSuffix 'P'
            buildConfigField "String", "API_KEY", "\"" + (properties.getProperty('prod_api_key') ?: "0000000000") + "\""
        }
        devKey {
            dimension 'key'
            versionNameSuffix 'D'
            buildConfigField "String", "API_KEY", "\"" + (properties.getProperty('dev_api_key') ?: "0000000000") + "\""
        }
        noKey {
            dimension 'key'
            versionNameSuffix 'N'
            buildConfigField "String", "API_KEY", "\"0000000000\""
        }
        dozeEnabled {
            dimension 'doze'
            versionNameSuffix 'D'
            buildConfigField "boolean", "DOZE_MANAGEMENT_ENABLED", "true"
        }
        dozeDisabled {
            dimension 'doze'
            versionNameSuffix 'N'
            buildConfigField "boolean", "DOZE_MANAGEMENT_ENABLED", "false"
        }
        billingEnabled {
            dimension 'billing'
            versionNameSuffix 'B'
            buildConfigField "boolean", "BILLING_ENABLED", "true"
            buildConfigField "String", "PREMIUM_API_KEY", "\"" + (properties.getProperty('prod_api_key')) + "\""
        }
        billingDisabled {
            dimension 'billing'
            versionNameSuffix 'N'
            buildConfigField "boolean", "BILLING_ENABLED", "false"
            buildConfigField "String", "PREMIUM_API_KEY", "\"0000000000\""
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.material
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.material.v1110
    implementation libs.volley
    implementation libs.androidx.activity
    implementation libs.gson
    implementation libs.androidx.work.runtime
    implementation libs.androidx.concurrent.futures
    implementation libs.billing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest

    annotationProcessor project(':annotationprocessor')
    implementation project(':annotationprocessor')
}